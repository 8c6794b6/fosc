#!/bin/sh
#|-*- mode: lisp -*-|#
#|
exec ros -Q -- $0 "$@"
|#

(ros:ensure-asdf)
(ql:quickload '(:fast-io) :silent t)

(defpackage :cov
  (:use :cl))

(in-package :cov)

(push :coverage *features*)

#+sbcl
(require :sb-cover)

(defun call-with-coverage (thunk)
  #+sbcl
  (with-compilation-unit
      (:policy '(optimize (sb-cover:store-coverage-data 3)))
    (funcall thunk)
    (sb-cover:report "/tmp/coverage-sbcl-fosc/"))
  #+ccl
  (progn
    (setq ccl:*compile-code-coverage* t)
    (funcall thunk)
    (ccl:report-coverage "/tmp/coverage-ccl-fosc/index.html")
    (setq ccl:*compile-code-coverage* nil)))

(defmacro with-coverage (&body body)
  `(call-with-coverage (lambda () ,@body)))

(defun main (&rest argv)
  (declare (ignorable argv))
  (unwind-protect
       (with-coverage
         (asdf:load-system :fosc/tests
                           :force '(:fosc :fosc/tests)
                           :force-not '(:fast-io))
         (asdf:test-system :fosc))
    (setf *features* (remove :coverage *features*))
    (delete-package :fosc/tests)
    (delete-package :fosc)
    (asdf:load-system :fosc
                      :force '(:fosc)
                      :force-not '(:fast-io))))
